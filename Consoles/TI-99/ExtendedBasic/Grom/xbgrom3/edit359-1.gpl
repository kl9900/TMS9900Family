********************************************************************************
       TITL 'EDIT-359'
*************************************************************
*                        GROM HEADER                        *
*************************************************************
       GROM >6000
       AORG 0
       DATA >AA02,>0100,0,USER,0,LINK1,0,0
*************************************************************
*    Branch table for routines in EDIT                      *
*************************************************************
       BR   AUTON
G6012  BR   TOPL15
       BR   INITPG
       BR   SPRINT            Initialize sprites.
       BR   $                 Was ILL1
       BR   TOPL10
G601C  BR   CHRTAB
       BR   SZRUN
       BR   $                 Was GETLNB
       BR   KILSYM
       BR   $                 Was CRUNCH
       BR   GETNB
       BR   GETNB2
       BR   GETCHR
       BR   GETLN
       BR   AUTO1
       DATA TOPL02
       BR   EDITLN
       BR   GRSUB1            Read from ERAM (use GREAD/VDP)
       BR   GWSUB             Write a few bytes to ERAM/VDP
*    Error and system messages
*      BASE 0,0,>300,>300,0,0,>60
MSGERR BYTE >A9,>AE,>80,>A5,>B2,>B2,>AF,>B2
*           In Error
MSGFST BYTE >07,>B2,>A5,>A1,>A4,>B9,>80,>8A
*            Ready
MSGBRK BYTE >0A,>A2,>B2,>A5,>A1,>AB,>B0,>AF,>A9,>AE,>B4
*            Breakpoint
MSGTA  BYTE >B4,>B2,>B9,>80,>A1,>A7,>A1,>A9,>AE
*           Try Again
MSGWRN BYTE >8A,>80,>B7,>A1,>B2,>AE,>A9,>AE,>A7
*            * Warning
MSG10  BYTE >10,>AE,>B5,>AD,>A5,>B2,>A9,>A3,>80
*            Numeric
       BYTE >AF,>B6,>A5,>B2,>A6,>AC,>AF,>B7
*           Overflow
MSG14  BYTE >0C,>B3,>B9,>AE,>B4,>A1,>B8,>80,>A5,>B2,>B2,>AF,>B2
*            Syntax Error
MSG16  BYTE >18,>A9,>AC,>AC,>A5,>A7,>A1,>AC,>80,>A1,>A6,>B4,>A5,>B2,>80
*            Illegal After
       BYTE >B3,>B5,>A2,>B0,>B2,>AF,>A7,>B2,>A1,>AD
*           Subprogram
MSG17  BYTE >10,>B5,>AE,>AD,>A1,>B4,>A3,>A8,>A5,>A4,>80
*           Unmatched
       BYTE >B1,>B5,>AF,>B4,>A5,>B3
*           Quotes
MSG19  BYTE >0D,>AE,>A1,>AD,>A5,>80,>B4,>AF,>AF,>80,>AC,>AF,>AE,>A7
*            Name Too Long
MSG24  BYTE >16,>B3,>B4,>B2,>A9,>AE,>A7,>8D,>AE,>B5,>AD,>A2,>A5,>B2,>80
*            String-Number
       BYTE >AD,>A9,>B3,>AD,>A1,>B4,>A3,>A8
*           Mismatch
MSG25  BYTE >11,>AF,>B0,>B4,>A9,>AF,>AE,>80,>A2,>A1,>B3,>A5,>80
*            Option Base
       BYTE >A5,>B2,>B2,>AF,>B2
*           Error
MSG28  BYTE >14,>A9,>AD,>B0,>B2,>AF,>B0,>A5,>B2,>AC,>B9,>80
*            Improperly
       BYTE >B5,>B3,>A5,>A4,>80,>AE,>A1,>AD,>A5
*           Used Name
MSG34  BYTE >16,>B5,>AE,>B2,>A5,>A3,>AF,>A7,>AE,>A9,>BA,>A5,>A4,>80
*            Unrecognized
       BYTE >A3,>A8,>A1,>B2,>A1,>A3,>B4,>A5,>B2
*           Character
MSG36  BYTE >0B,>A9,>AD,>A1,>A7,>A5,>80,>A5,>B2,>B2,>AF,>B2
*            Image Error
MSG39  BYTE >0B,>AD,>A5,>AD,>AF,>B2,>B9,>80,>A6,>B5,>AC,>AC
*            Memory Full
MSG40  BYTE >0E,>B3,>B4,>A1,>A3,>AB,>80,>AF,>B6,>A5,>B2,>A6,>AC,>AF,>B7
*            Stack Overflow
MSG43  BYTE >10,>AE,>A5,>B8,>B4,>80,>B7,>A9,>B4,>A8,>AF,>B5,>B4,>80
*            NEXT Without
       BYTE >A6,>AF,>B2
*           FOR
MSG44  BYTE >10,>A6,>AF,>B2,>8D,>AE,>A5,>B8,>B4,>80
*           FOR-NEXT
       BYTE >AE,>A5,>B3,>B4,>A9,>AE,>A7
*           Nesting
MSG47  BYTE >15,>AD,>B5,>B3,>B4,>80,>A2,>A5,>80,>A9,>AE
*            Must be in
       BYTE >80,>B3,>B5,>A2,>B0,>B2,>AF,>A7,>B2,>A1,>AD
*            Subprogram
MSG48  BYTE >19,>B2,>A5,>A3,>B5,>B2,>B3,>A9,>B6,>A5,>80
*           Recursive
       BYTE >B3,>B5,>A2,>B0,>B2,>AF,>A7,>B2,>A1,>AD,>80,>A3,>A1,>AC,>AC
*           Subprogram Call
MSG49  BYTE >0E,>AD,>A9,>B3,>B3,>A9,>AE,>A7,>80,>B3,>B5,>A2,>A5,>AE,>A4
*            Missing Subend
MSG51  BYTE >14,>B2,>A5,>B4,>B5,>B2,>AE,>80,>B7,>A9,>B4,>A8,>AF,>B5,>B4
*            RETURN Without
       BYTE >80,>A7,>AF,>B3,>B5,>A2
*           GOSUB
MSG54  BYTE >10,>B3,>B4,>B2,>A9,>AE,>A7,>80
*            String
       BYTE >B4,>B2,>B5,>AE,>A3,>A1,>B4,>A5,>A4
*           Truncated
MSG57  BYTE >0D,>A2,>A1,>A4,>80,>B3,>B5,>A2,>B3,>A3,>B2,>A9,>B0,>B4
*            Bad Subscript
MSG60  BYTE >0E,>AC,>A9,>AE,>A5,>80,>AE,>AF,>B4,>80,>A6,>AF,>B5,>AE,>A4
*           Line Not Found
MSG61  BYTE >0F,>A2,>A1,>A4,>80,>AC,>A9,>AE,>A5,>80
*           Bad Line
       BYTE >AE,>B5,>AD,>A2,>A5,>B2
*           Number
MSG67  BYTE >0E,>A3,>A1,>AE,>87,>B4,>80,>A3,>AF,>AE,>B4,>A9,>AE,>B5,>A5
*            Can't Continue
MSG69  BYTE >1A,>A3,>AF,>AD,>AD,>A1,>AE,>A4,>80
*            Command
       BYTE >A9,>AC,>AC,>A5,>A7,>A1,>AC,>80,>A9,>AE,>80
*           Illegal in
       BYTE >B0,>B2,>AF,>A7,>B2,>A1,>AD
*           Program
MSG70  BYTE >17,>AF,>AE,>AC,>B9,>80,>AC,>A5,>A7,>A1,>AC,>80
*            Only Legal
       BYTE >A9,>AE,>80,>A1,>80,>B0,>B2,>AF,>A7,>B2,>A1,>AD
*           in a Program
MSG74  BYTE >0C,>A2,>A1,>A4,>80,>A1,>B2,>A7,>B5,>AD,>A5,>AE,>B4
*            Bad Argument
MSG78  BYTE >12,>AE,>AF,>80,>B0,>B2,>AF,>A7,>B2,>A1,>AD
*            No Program
       BYTE >80,>B0,>B2,>A5,>B3,>A5,>AE,>B4
*            Present
MSG79  BYTE >09,>A2,>A1,>A4,>80,>B6,>A1,>AC,>B5,>A5
*            Bad Value
MSG81  BYTE >17,>A9,>AE,>A3,>AF,>B2,>B2,>A5,>A3,>B4,>80
*            Incorrect
       BYTE >A1,>B2,>A7,>B5,>AD,>A5,>AE,>B4,>80,>AC,>A9,>B3,>B4
*           Argument List
MSG83  BYTE >0B,>A9,>AE,>B0,>B5,>B4,>80,>A5,>B2,>B2,>AF,>B2
*            Input Error
MSG84  BYTE >0A,>A4,>A1,>B4,>A1,>80,>A5,>B2,>B2,>AF,>B2
*            Data Error
MSG97  BYTE >14,>B0,>B2,>AF,>B4,>A5,>A3,>B4,>A9,>AF,>AE,>80
*            Protection
       BYTE >B6,>A9,>AF,>AC,>A1,>B4,>A9,>AF,>AE
*           Violation
MSG109 BYTE >0A,>A6,>A9,>AC,>A5,>80,>A5,>B2,>B2,>AF,>B2
*            File Error
MSG130 BYTE >09,>A9,>8F,>AF,>80,>A5,>B2,>B2,>AF,>B2
*            I/O Error
MSG135 BYTE >14,>B3,>B5,>A2,>B0,>B2,>AF,>A7,>B2,>A1,>AD,>80
*            Subprogram
       BYTE >AE,>AF,>B4,>80,>A6,>AF,>B5,>AE,>A4
*            Not Found
MSG62  BYTE >0D,>AC,>A9,>AE,>A5,>80,>B4,>AF,>AF,>80,>AC,>AF,>AE,>A7
*            Line Too Long
MSGFRE BYTE >A2,>B9,>B4,>A5,>B3,>80,>A6,>B2,>A5,>A5
*           Bytes Free
MSGSFR BYTE >A2,>B9,>B4,>A5,>B3,>80,>AF,>A6,>80,>B3,>B4,>A1,>A3,>AB,>80
*           Bytes of Stack
       BYTE >A6,>B2,>A5,>A5
*           Free
MSGGFR BYTE >A2,>B9,>B4,>A5,>B3,>80,>AF,>A6,>80,>B0,>B2,>AF,>A7,>B2,>A1
*           Bytes of Program
       BYTE >AD
MSGGF1 BYTE >B3,>B0,>A1,>A3,>A5,>80,>A6,>B2,>A5,>A5
*           Space Free
MSGCIS BYTE >B5,>A4,>A6,>80,>B2,>A5,>A6,>B3,>80,>A9,>B4,>B3,>A5,>AC,>A6
*           UDF Refs Itself
MSGCF  BYTE >A3,>A1,>AC,>AC,>A5,>A4,>80,>A6,>B2,>AF,>AD
*           Called From
MSG56  BYTE >16,>B3,>B0,>A5,>A5,>A3,>A8,>80,>B3,>B4,>B2,>A9,>AE,>A7,>80
*            Speech String
       BYTE >B4,>AF,>AF,>80,>AC,>AF,>AE,>A7
*           Too Long
*      BASE 0,0,>0300,>0300,0,0,0
USER   DATA 0,TOPLEV
       BYTE 17
       TEXT 'TI EXTENDED BASIC'
DSCLOD BYTE 9
       TEXT 'DSK1.LOAD'
       BYTE 0
SPCCHR BYTE >00,>7C,>7C,>7C,>7C,>7C,>7C,>7C     * CURSOR CHARACTER
       DATA 0,0,0,0                             * EDGE CHARACTER
VDPREG BYTE >E0,>00,>20,>00,>06,>00
*************************************************************
*            START OF BASIC INTERPETER                      *
*************************************************************
TOPLEV CLR  V@>0370           Initialize temp area
       MOVE 77,V@>0370,V@>0371
       ST   5,@KEYBD          Select full keyboard
       SCAN
       CZ   @KEYBD
       BR   G6388             99/4A Console?
       ST   >01,V@CONFLG      Select 99/4A console
G6388  CLR  @KEYBD
       DST  NLNADD,V@BUFSRT   Initialize edit-buffer start
       DST  NLNADD,V@BUFEND   Initialize edit-buffer end
       MOVE 2,G@ATNZZ,@INTRIN  Get address of ATNZZ
       AND  >1F,@INTRIN       Throw away the BR opcode
       DADD >5B,@INTRIN       Address of polynomial constants
       ST   >FF,V@LODFLG      indicate try auto-boot
*-----------------------------------------------------------*
* Add the following line for fixing "MEMORY FULL" error     *
* occurring during MERGE execution will leave the file open *
* to disk DSR bug, 5/19/81                                  *
SZNEW  CLR  V@MRGPAB          Initialize merged temporary   *
*                              for PAB pointer              *
*-----------------------------------------------------------*
       ST   RSTK,@SUBSTK      Load base of subroutine stack
       CALL CHRTA2            Load character table
       CLR  @FLAG             Initialize flag byte
       DCLR @BUFLEV           Initialize crunch buffer level
       CALL CLSALL            Close all open files
       CLR  @DATA             Initialize READ/DATA pointer
       DST  VRAMVS,@VSPTR     Initialize base of value stack
       DST  @VSPTR,@STVSPT    Save in permanent base
       DST  @VSPTR,V@SAVEVP
       CALL INITPG            Initialize program & s.t.
       CALL INTRND            Initialize random number
       CZ   V@LODFLG
       BS   TOPL02            If need auto-boot
       CLR  V@LODFLG          Won't ever need to do again
       CALL AUTOLD            Attempt an auto-boot
*     Label TOPL02 is used by auto-boot in detection of errors.
ERRRDY EQU  $
TOPL02 CALL G6A84             Say READY
       BYTE 0               *  returns to TOPL15
TOPL05 CALL INITPG            Initialize program space
TOPL10 CALL KILSYM            Kill the symbol table
TOPL15 AND  >F7,@FLAG         If error in UDF execution
       ST   5,@KEYBD          Select full keyboard
       SCAN
       CLR  @KEYBD
TOPL20 ST   RSTK,@SUBSTK      Initialize subroutine stack
TOPL25 DST  NLNADD,@VARW      Screen addr = lower left corner
       CLR  @RAMFLG           Clear the RAMFLG
       CLR  @PRGFLG           Make sure not in program mode
* Check for auto-num mode
       CLOG >01,@FLAG         If auto-num on
       BS   TOPL35
       DADD @CURINC,@CURLIN   Generate new line number
       CGE  0,@CURLIN         >32767?
       BS   TOPL30
       AND  >FE,@FLAG         If out of range->exit auto-num
       B    TOPL35            Merge in below
* Must be a long branch!!
TOPL30 DCEQ @ENLN,@STLN       Line might exist
       BS   G6412
       DST  @CURLIN,@FAC      Ready for program search
       XML  SPEED
       BYTE SEETWO          * Search for existence of line
       BS   EDTZ05            COND set = line found
G6412  XML  SCROLL            Scroll to the next line
       DST  @CURLIN,@ARG2     New line #
       CALL G6A7C             Display the line number
       DINC @VARW             Following by a space
       BR   G6420
TOPL35 XML  SCROLL            Scroll the screen
G6420  ST   >9E,V@NLNADD-1    Display the prompt character
       CALL G6A76             Read in a line
       CALL SAVLIN            Save input line for recall     <<<<<<<<<<<<<<<<<<<
*    Crunch the input line
       CLR  @ERRCOD           Assume no-error return
       DST  CRNBUF,@RAMPTR    Initialize crunch pointer
       XML  CRUNCH            CRUNCH the input line
       BYTE 0              *  Normal crunch mode
TOPL42 CASE @ERRCOD+1
       BR   TOPL45            No error detected
       BR   ERRSYN            *SYNTAX ERROR
       BR   ERRBLN            *BAD LINE NUMBER
       BR   ERRLTL            *LINE TOO LONG
       BR   ERRNTL            *NAME TOO LONG
       BR   ERRNQS            *UNMATCHED QUOTES
       BR   ERRCIP            *COMMAND ILLEGAL IN PROGRAM
       BR   ERRIVN            *UNRECOGNIZED CHARACTER
TOPL45 DCZ  @FAC              Line # present
       BS   TOPL55
       CLOG >01,@FLAG         Not AUTONUM
       BR   G645B
       CEQ  >0D,@RKEY         Must be up or down
       BS   G645B
       CEQ  >01,@CHAT         Start EDIT mode
       BR   G645B
       B    EDTZZ0
G645B  CALL EDITLN            EDIT the line into the program
       BS   TOPL25            If didn't change the line
       BR   TOPL10
*    Jump always
TOPL55 CEQ  >01,@CHAT         If blank line - ignore
       BS   TOPL25
       CEQ  >EB,V@CRNBUF
       BS   SZSIZE
       CH   >08,V@CRNBUF      If imperative
       BS   SZRUN4
       DST  CRNBUF+1,@PGMPTR  Anticipate usage of PGMCHR
       XML  PGMCHR            Prepare CHAT for OLD and SAVE
       CASE V@CRNBUF          Select the keyword
       BR   SZNEW             NEW                 0
       BR   SZCONT            CONTINUE            1
       BR   SZLIST            LIST                2
       BR   SZBYE             BYE                 3
       BR   SZNUM             NUMBER              4
       BR   SZOLD             OLD                 5
       BR   SZRES             RESEQUENCE          6
       BR   SZSAVE            SAVE                7
       BR   SZMERG            MERGE               8
*    AUTO-BOOT - attempt a ---->   RUN "DSK1.LOAD"
AUTOLD MOVE 11,G@DSCLOD,V@CRNBUF
       DST  CRNBUF,@PGMPTR    DSK1.LOAD is in crunch buffer
       BR   SZRUNL            Go to the RUN "NAME" CODE
********************************* RUN *********************************
SZRUN  CEQ  >C7,@CHAT         Ready for 'RUN "NAME" ----
       BR   G64BF
SZRUNL DST  @PGMPTR,@FAC14    Save pointer to name
       XML  PGMCHR            Get the length of the string
       ST   @CHAT,@FAC13      Put it in FAC13
       CLR  @FAC12            Make it a double byte
       DADD @FAC12,@PGMPTR    Skip the string
       XML  PGMCHR            To see there is line no. ahead
       CALL G8024             Only RUN "NAME" ?
       BR   ERRSYN            No - junk on end so error
       ST   STRINZ,@CHAT      Prepare for LOAD routine
       DST  @FAC14,@PGMPTR    Restore the saved PGMPTR
       CALL OLD1              Load the program
       BR   SZRUN0            Go ahead from here
*                              No RUN "NAME" : just run the
*                              current program in memory
G64BF  CEQ  >C9,@CHAT         Is there a line # after RUN?
       BR   G64D5
       XML  PGMCHR            Get the line number
       ST   @CHAT,@FAC        Put it in FAC for SEETWO
       XML  PGMCHR
       ST   @CHAT,@FAC1
       XML  PGMCHR            Should be EOS now
       CALL G8024             Is it?
       BS   SZRUN2            Yes - Go ahead from here
*                              Just 'RUN'
G64D5  CALL G8024             Should be EOS now
       BR   ERRSYN            No-SYNTAX ERROR
SZRUN0 DCEQ @ENLN,@STLN       Refuse without program
       BS   ILLST
       DST  @ENLN,V@START     Defualt to beginning
       DSUB 3,V@START         Offset into the table
       BR   SZRUN1            Merge in below
*    Jump always
SZRUN2 DCEQ @ENLN,@STLN       Refuse without program
       BR   G64F9
ILLST  XML  SCROLL            Scroll the screen for message
       CLR  @PRGFLG           Prevent line # printing
WRNNPP CALL G6A82
       BYTE 29                * NO PROGRAM PRESENT
       BR   TOPL15
*    Condition can never be set since line 0 is prohibited
G64F9  XML  SPEED
       BYTE SEETWO          * Find the line in the program
       BR   ERRLNF            * LINE NOT FOUND
       DST  @EXTRAM,V@START   Program run starts here
SZRUN1 CLR  @PRGFLG           No line #s if error in CLSALL
       CALL CLSALL            Close any open files
       DEC  @PRGFLG           Put it back in execution
       ST   @RAMTOP+1,@RAMFLG  Set/reset RAMFLG flag -- when
       DCLR V@SEXTRM           in program mode & ERAM exists
       DCLR V@ERRLN           Disallow CONTINUE after RUN
       CALL KILSYM            Reset ERR handling to defualt
       ST   RSTK,@SUBSTK      Set the stack empty
SZRUN4 B    G6A70
EDTZ05 B    EDTZ00
**************************** CONTINUE ***********************
SZCONT CALL GETNB             Check for END-OF-LINE
       BR   ERRSY1            Junk on end of command
       DCZ  V@SEXTRM          If can continue
       BS   ERRCC
       XML  SCROLL
       DST  V@SEXTRM,@EXTRAM  Copy old line table pointer
       DST  V@SPGMPT,@PGMPTR  Copy old text pointer
       DST  V@SBUFLV,@BUFLEV  Copy old buffer level
       DST  V@SLSUBP,@LSUBP   Copy last subprogram on stack
       OR   V@SFLAG,@FLAG     Restore on-warning/break bits
G6540  DCH  V@SAVEVP,@VSPTR   While extra on stack
       BR   G654A
       XML  VPOP              Pop them off
       BR   G6540
G654A  ST   >FF,@PRGFLG       Idicate program mode
       ST   @RAMTOP+1,@RAMFLG  Set/reset RAMFLG flag --- when
*                              in program mode & ERAM exists
       DCLR V@SEXTRM          Prevent unauthorized CONTINUE
       DST  VRAMVS,V@SAVEVP   Init for program completion
       XML  CONTIN            Resume normal execution
ERRCC  CALL G6A84             Indicate error
       BYTE 25              * "* CAN'T CONTINUE"
**************************** NUMBER *************************
*-----------------------------------------------------------*
* Fix NUMBER command cause XB goes into a loop displaying   *
* *PROTECTION VIOLATION when a PROTECTED program is in      *
* memory bug, add the following line after label SZNEW      *
SZNUM  CLOG >80,@FLAG         Check PROTEDTION VIOLATION    *
       BR   ERRPV                                           *
*-----------------------------------------------------------*
       CALL AUTON             Get start line # and increment
       OR   >01,@FLAG         Set AUTONUM bit for future use
       DST  NLNADD,@VARW      Initialize screen address
       BR   TOPL30            Jump back into it
*    Jump always
*************************************************************
* AUTON - scans the NUM, LIST and RES commands for line     *
* numbers. Leaves 1st line number in CURLIN and 2nd line    *
* number in CURINC. AUTON is entry point from NUM to defualt*
* to 100,10          AUTON is entry point for LIST.         *
*************************************************************
AUTON  DST  100,@CURLIN       Defualt start
       DST  10,@CURINC        Defualt increment
       ST   COMMA,@VARC       Comma is the separator
AUTO1  DDEC @VARW             Don't miss the first character
       CALL GETNB             Get 1st character after keyword
       BS   AUTO2             If end of line
       CALL GETLN             Try to get a line number
       CZ   @BYTES            If digits gotten
       BS   G658D
       DST  @FAC,@CURLIN      Set initial
G658D  CALL GETNB2            Allow spaces before separator
       DCH  @VARA,@VARW       Check end of line
       BS   AUTO2
       CEQ  @VARC,@CHAT       If not correct separator
       BR   ERRSY1
       CALL GETNB             Get char after separator
       BS   AUTO2             If end of line
       CALL GETLN             Try to get 2nd number
       CZ   @BYTES            If digits gotten
       BS   G65A9
       DST  @FAC,@CURINC      Save the increment
G65A9  CALL GETNB2            Check EOL
       BR   ERRSY1            NOT EOL: SYNTAX ERROR
AUTO2  RTN
*************************** SAVE ****************************
SZSAVE DCEQ @ENLN,@STLN       If no program
       BS   ILLST
       B    SAVE
*************************** OLD *****************************
SZOLD  B    OLD
*************************** BYE *****************************
SZBYE  CALL CLSALL            Properly close all files
       EXIT                   Return to MONITOR
*************************** LIST ****************************
SZLIST DCEQ @ENLN,@STLN       Refuse LIST without prrogram
       BS   ILLST
       B    LIST              LIST the program
*************************** MERGE ***************************
SZMERG B    MERGE                                            <<<<<<<<<<<<<<<<<<<
*************************** SIZE ****************************
SZSIZE CZ   V@CRNBUF+1        Must have EOL
       BR   ERRSYN
       XML  COMPCT            Garbage collect to free space
       DST  @STREND,@ARG2     Get end of string space
       DSUB @VSPTR,@ARG2      Subtract stack pointer
       DSUB 63,@ARG2          Require 64-byte buffer
       GT                     If less then 64 bytes left
       BS   G65DF
       DCLR @ARG2             Then indicate zero
G65DF  XML  SCROLL            Scroll the screen
       DST  NLNADD+2,@VARW    Begin a new line
       CALL G6A7C             Display the number
       CZ   @RAMTOP           If no ERAM present
       BR   G65F7
       MOVE 10,G@MSGFRE,V@1(@VARW)
       BR   G6621
G65F7  MOVE 19,G@MSGSFR,V@1(@VARW)
       XML  SCROLL            Scroll the screen
       DST  NLNADD+2,@VARW    Beginning of line
       DST  @RAMFRE,@ARG2     Calculate space in ERAM
       DSUB CPUBAS-1,@ARG2    Subtract base
       CALL G6A7C             Display the number
       MOVE 16,G@MSGGFR,V@1(@VARW)
       XML  SCROLL
       MOVE 10,G@MSGGF1,V@NLNADD+4
G6621  XML  SCROLL            Scroll the screen
       BR   TOPL15            Return to top-level
************************** RESEQUENCE ***********************
SZRES  DCEQ @ENLN,@STLN       If no program
       BS   ILLST
       CALL AUTON             Get start line & increment
       DST  @ENLN,@FAC        Compute # of increments required
       DSUB @STLN,@FAC        Actual number of lines - 1
       DSRL 2,@FAC            Also takes care of this ^^^
       DMUL @CURINC,@FAC      Compute space taken by increments      <<<<<<<<<<<
       DCZ  @FAC              Bad line number
       BR   ERRBLN
       DADD @FAC2,@CURLIN     Compute highest address used
       CARRY                  Watch out for overflow
       BS   ERRBLN
       CH   >7F,@CURLIN       Overflow is > 32767
       BS   ERRBLN
       ST   @RAMTOP+1,@RAMFLG  Set/reset RAMFLG to use PGMCHR
       CLR  @ARG4             To be used for double add
       DST  @ENLN,@PGMPTR     Start at end of program
       DST  @>8370,@VAR0      Assume VDP-top
       CZ   @RAMFLG           But if ERAM exists
       BS   G665F
       DST  @RAMTOP,@VAR0     Top for ERAM
G665F  DINCT @PGMPTR          Skip EOL and count
G6661  XML  PGMCHR            VDP RAM or ERAM
       CEQ  >C7,@CHAT         Skip strings
       BS   SEQZ2
       CEQ  >C8,@CHAT         If numeric
       BR   G6677
SEQZ2  XML  PGMCHR            Get next token (count)
       ST   @CHAT,@ARG5       For double add
       DADD @ARG4,@PGMPTR     Up to end of string
       BR   G66AA
G6677  CEQ  >C9,@CHAT         Check for line #
       BR   G66AA
       CALL GRSUB2            Get the line # in the text
       BYTE PGMPTR          * @PGMPTR : Source addr on ERAM/VDP
       DST  @EEE1,@FAC8       Save it temporary place
       DST  @CURLIN,@ARG2     Set for searching
       DST  @STLN,@ARG        Compare with entries in table
G6689  CALL GRSUB3            Read the line # fromn ERAM
*                              (use GREAD1) or VDP, reset
*                              possible breakpoint too
       BYTE ARG             * @ARG : Source addr on ERAM/VDP
       DCEQ @EEE1,@FAC8
       BS   SEQZ3
       DSUB @CURINC,@ARG2     Update new line #
       DADD 4,@ARG            And entry in line # table
       DCH  @ENLN,@ARG        Stop if end of table
       BR   G6689
       DST  >7FFF,@ARG2       Default = 32767
SEQZ3  CALL GWSUB             Write a few bytes of data
*                 @PGMPTR : Destination address on ERAM/VDP
*                 @ARG2   : Data
*                 2       : Byte count
       BYTE PGMPTR,ARG2,2
       DINCT @PGMPTR          Pass two byte line # in text
G66AA  DCLR @>83D6            Reset VDP timeout
       DCHE @VAR0,@PGMPTR     And on end of program
       BR   G6661
*  Now update the line # table itself
       DST  @STLN,@FAC        Start at beginning of table
       DST  @CURLIN,@ARG      With start address off course
G66B8  CALL GWSUB             Write a few bytes of data to
*                              ERAM (use GWRITE) or VDP
*                 @FAC   : Destination address on ERAM/VDP
*                 @ARG   : Data
*                 2      : Byte count
       BYTE FAC,ARG,2
       DSUB @CURINC,@ARG      Compute next line #
       DADD 4,@FAC            And next entry in line # table
       DCH  @ENLN,@FAC        Stop at end of line # table
       BR   G66B8
       CLR  @RAMFLG           Restore the ERAM flag
       BR   TOPL25
 
